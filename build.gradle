buildscript {
    ext {
        springBootVersion = '1.5.8.RELEASE'
        gradleDockerVersion = '1.2'
        gradleDMVersion = '1.0.3.RELEASE'

    }
    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
        maven { url 'https://repo.spring.io/libs-milestone' }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("se.transmode.gradle:gradle-docker:${gradleDockerVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${gradleDMVersion}")
    }
}

ext {
    springCloudVersion = 'Dalston.SR4'
    springCtxSupportVersion = '5.0.0.RELEASE'
    jjwtVersion = '0.9.0'
    jasperVersion = '6.4.0'
    olap4jVersion = '1.2.0'
    oracleVersion =  '12.1.0.2.0'
    h2Version = '1.4.196'
    jasperVersion = '6.4.3'
    itextVersion = '2.0.7'
    poiVersion = '3.17'
    sqlserverVersion = '4.0'
    lombokVersion = '1.16.18'
    swaggerVersion = '2.7.0'
    easyRulesVersion = '3.0.0'
    guavaVersion = '23.2-jre'
    adminVersion = '1.5.4'
    immutableVersion = '2.5.5'
    mysqlVersion = '5.1.30'
    mybatisVersion = '1.3.1'
}

allprojects {
    // docker çš„ group
    group = 'wpcfan'
    repositories() {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
        maven { url 'https://repo.spring.io/libs-milestone' }
        mavenCentral()
        jcenter()
    }
    apply plugin: 'java'
    apply plugin: 'idea'
}

subprojects {
    version = "0.0.1"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'docker'
    dependencies {
//        compile("com.google.guava:guava:${guavaVersion}")
//        compile("org.immutables:value:${immutableVersion}")
        compile('org.projectlombok:lombok')
        runtime('org.springframework.boot:spring-boot-devtools')
        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('org.springframework.security:spring-security-test')
    }
    /*
     * This task builds the docker image by copying the output of the "jar" gradle command
     * and moving it to the "build/docker" directory which is used as the staging directory
     * by the docker gradle plugin. We then build the docker image by invoking the appropriate
     * Dockerfile.
     */
    task buildDocker(type: Docker, dependsOn: build) {
        push = false
        applicationName = jar.baseName
        tagVersion = version
        dockerfile = file('src/main/docker/Dockerfile')
        doFirst {
            // Copy the built fat jar to the Docker plugin staging directory
            copy {
                from jar
                into stageDir
            }
            // Copy the run.sh file to the Docker plugin staging directory
            copy {
                from "${project.buildDir}/resources/main/run.sh"
                into stageDir
            }
        }
    }
}
